/**
 * @title DAO
 * @notice This contract implements a decentralized autonomous organization (DAO) where members can create and vote on proposals.
 */
@compiler >= 6

include "String.aes"
include "List.aes"
include "Pair.aes"


payable contract DAO = 

    /**
     * @dev Structure to store the state of the DAO.
     */
    record state = {
        name: string,               // The name of the DAO
        id: string,
        description: string,        // A brief description of the DAO
        image: string,              // URL of the DAO's image
        socials: list(socialUrl),      // List of social media links associated with the DAO
        votingTime: int,            // Duration of the voting period for proposals (in milliseconds)
        quorum: int,                // Percentage of members required for a proposal to be considered valid
        proposals: map(int, proposal),  // Mapping of proposal IDs to their respective details
        totalProposals: int,        // Total number of proposals created
        totalVotes: int,            // Total number of votes cast
        members: list(address),     // List of addresses representing members of the DAO
        activities: map(address, userActivities)}  // Mapping of member addresses to their respective voting activities

    /**
     * @dev Structure to represent a proposal.
     */
    record proposal = {
        id: int,                    // Unique identifier for the proposal
        proposer: address,          // Address of the member who proposed the action
        proposalType: string,       // Type of proposal (e.g., addMember, removeMember, setQuorum, setVotingTime)
        description: string,        // Description of the proposal
        value: int,                 // Value associated with the proposal (if applicable)
        target: address,            // Target address affected by the proposal (if applicable)
        info: infoPayload,
        startTime: int,              // Timestamp indicating the start of the voting period for the proposal
        endTime: int,               // Timestamp indicating the end of the voting period for the proposal
        votesFor: int,              // Number of votes in favor of the proposal
        votesAgainst: int,          // Number of votes against the proposal
        isExecuted: bool,           // Flag indicating whether the proposal has been executed
        votes: list(vote),
        daoName: string,
        daoId: string,
        hasVoted: map(address, bool)}  // Mapping of member addresses to their vote status for the proposal


    record userActivities = {
        account: address,
        voteCasted: int,
        proposalsCreated: int,
        proposalsExecuted: int}

    /**
     * @dev Structure to represent information about the DAO.
     */
    record info = {
        name: string,               // The name of the DAO
        id: string,                 // Unique identifier for the DAO
        description: string,        // A brief description of the DAO
        contractAddress: DAO,       // Address of the DAO contract
        account: address,           // Address of the DAO contract
        image: string,              // URL of the DAO's image
        socials: list(socialUrl),  // List of social media links associated with the DAO
        quorum: int,                // Percentage of members required for a proposal to be considered valid
        votingTime: int,            // Duration of the voting period for proposals (in milliseconds)
        members: list(address),     // List of addresses representing members of the DAO
        totalProposals: int,        // Total number of proposals created
        totalVotes: int,            // Total number of votes cast
        balance: int,
        activeProposals: int}       // Number of active proposals (i.e., proposals with ongoing voting)

    /**
    * @dev Structure to represent a vote in a proposal.
    * @field account The wallet address of the voter.
    * @field support Boolean indicating whether the vote is in favor (true) or against (false).
    */
    record socialUrl = {
        name: string,
        url: string}
    /**
    * @dev Structure to represent a vote in a proposal.
    * @field account The wallet address of the voter.
    * @field support Boolean indicating whether the vote is in favor (true) or against (false).
    */
    record vote = {
        account: address,
        support: bool}

    /**
    * @dev Structure used for passing information as a payload in proposals.
    * @field name Name relevant to the proposal, such as updated name of the DAO.
    * @field socials List of updated social media URLs, if the proposal involves updating social links.
    * @field image New image URL if the proposal involves updating the DAO's image.
    */
    // Info payload
    record infoPayload = {
        name: string,
        socials: list(socialUrl),
        image: string}

    let validProposalTypes = ["updateName", "updateSocials", "updateImage", "addMember", "removeMember", "quorum"," voteTime", "transfer"]


    /**
     * @notice Initializes the DAO with the provided parameters.
     * @param _name The name of the DAO.
     * @param _description A brief description of the DAO.
     * @param _image URL of the DAO's image.
     * @param _socials List of social media links associated with the DAO.
     * @param _creator Address of the creator of the DAO.
     * @param _initialMembers List of addresses representing initial members of the DAO.
     * @return The initial state of the DAO.
     */
    stateful entrypoint init(_name: string, _id: string, _description: string, _image: string, _socials: list(socialUrl), _creator: address, _initialMembers: list(address), _votingTime: int, _quorum: int): state = 
        require(_quorum > 0 && _quorum =< 100, "Quorum must be between 0 and 100")
        {
            name = _name,
            id = _id,
            description = _description,
            image = _image,
            socials = _socials,
            votingTime = _votingTime,
            quorum = _quorum,
            proposals = {},
            totalProposals = 0,
            totalVotes = 0,
            members = _creator :: _initialMembers,
            activities = {}}

    /**
     * @notice Retrieves information about the DAO.
     * @return Information about the DAO.
     */
    public entrypoint getInfo(): info = 
        {
            name = state.name,
            id = state.id,
            description = state.description,
            contractAddress = Address.to_contract(Contract.address): DAO,
            account = Contract.address,
            image = state.image,
            socials = state.socials,
            quorum = state.quorum,
            votingTime = state.votingTime,
            members = state.members,
            totalProposals = state.totalProposals,
            totalVotes = state.totalVotes,
            activeProposals = List.length(List.filter((x) => Pair.snd(x).endTime > Chain.timestamp, Map.to_list(state.proposals))),
            balance = Chain.balance(Contract.address)}

    /**
     * @notice Retrieves all proposals.
     * @return List of all proposals.
     */
    public entrypoint getProposals(): list(proposal) = 
        List.map((x) => Pair.snd(x), Map.to_list(state.proposals))

    /**
     * @notice Retrieves active proposals.
     * @return List of active proposals.
     */
    public entrypoint getActiveProposals(): list(proposal) = 
        List.filter((p) => p.endTime > Chain.timestamp, getProposals())

    /**
     * @notice Retrieves details of a specific proposal.
     * @param id The ID of the proposal.
     * @return Details of the specified proposal.
     */
    public entrypoint getProposal(id: int): proposal = 
        switch(Map.lookup(id, state.proposals))
            None => abort("Invalid proposal id")
            Some(p) => p

    /**
     * @notice Checks if an address is a member of the DAO.
     * @param account The address to check.
     * @return True if the address is a member, otherwise false.
     */
     
    public entrypoint isMember(account: address): bool =
        List.contains(account, state.members)
    /**
    * @notice Retrieves the activity record for a specific member.
    * @param account The address of the member whose activities are to be retrieved.
    * @return The activity record containing details of votes casted, proposals created, and proposals executed by the member.
    */
    public entrypoint getMemberActivities(account: address): userActivities =
        Map.lookup_default(account, state.activities, { account = account, voteCasted = 0, proposalsCreated = 0, proposalsExecuted = 0})
    /**
    * @notice Retrieves the activity record for a specific member.
    * @param account The address of the member whose activities are to be retrieved.
    * @return The activity record containing details of votes casted, proposals created, and proposals executed by the member.
    */
    public entrypoint getAllMembersActivities(): list(userActivities) =
        List.map((x) => Pair.snd(x), Map.to_list(state.activities))

    /**
     * @notice Creates a new proposal.
     * @param _proposalType Type of proposal.
     * @param _description Description of the proposal.
     * @param _value Value associated with the proposal.
     * @param _target Target address affected by the proposal.
     * @return The ID of the newly created proposal.
     */
    public stateful entrypoint createProposal(_proposalType: string, _description: string, _value: int, _target: address, _info: infoPayload) = 
        require(isMember(Call.caller), "You're not a member of this DAO")
        require(List.contains(_proposalType, validProposalTypes), "Invalid proposal type")
        let proposalId : int = state.totalProposals
        // Check for valid proposal type
        let newProposal : proposal = {
            id = proposalId,
            proposer = Call.caller,
            proposalType = _proposalType,
            description = _description,
            value = _value,
            target= _target,
            startTime = Chain.timestamp,
            endTime = Chain.timestamp + state.votingTime,
            votesFor = 0,
            votes = [],
            votesAgainst = 0,
            isExecuted = false,
            daoName = state.name,
            daoId = state.id,
            info = _info,
            hasVoted = {}}
        put(state{proposals[proposalId] = newProposal, totalProposals = proposalId + 1})
        let activities : userActivities = Map.lookup_default(Call.caller, state.activities, {account = Call.caller, proposalsCreated = 0, proposalsExecuted = 0, voteCasted = 0})
        put(state{activities[Call.caller] = activities{proposalsCreated = activities.proposalsCreated + 1}})
        newProposal

    /**
     * @notice Casts a vote in favor of a proposal.
     * @param proposalId The ID of the proposal to vote for.
     */
    public stateful entrypoint voteFor(proposalId: int) = 
        require(isMember(Call.caller), "You're not a member of this DAO")
        require(proposalId < state.totalProposals, "Invalid proposal id")
        let proposal = state.proposals[proposalId]
        require(!Map.lookup_default(Call.caller, proposal.hasVoted, false), "You've already voted!")
        require(Chain.timestamp < proposal.endTime, "Proposal has ended!")

        let userVote: vote = {
            account = Call.caller,
            support = true}
        let updatedProposal = proposal{votesFor @ v = v + 1, votes @ vts = userVote :: vts}
        put(state{proposals[proposalId] = updatedProposal, totalVotes = state.totalVotes + 1})
        put(state{proposals[proposalId].hasVoted[Call.caller] = true})
        let activities : userActivities = Map.lookup_default(Call.caller, state.activities, {account = Call.caller, proposalsCreated = 0, proposalsExecuted = 0, voteCasted = 0})
        put(state{activities[Call.caller] = activities{proposalsCreated = activities.voteCasted + 1}})
        updatedProposal

    /**
     * @notice Casts a vote against a proposal.
     * @param proposalId The ID of the proposal to vote against.
     */
    public stateful entrypoint voteAgainst(proposalId: int) = 
        require(isMember(Call.caller), "You're not a member of this DAO")
        require(proposalId < state.totalProposals, "Invalid proposal id")
        let proposal = state.proposals[proposalId]
        require(!Map.lookup_default(Call.caller, proposal.hasVoted, false), "You've already voted!")
        require(Chain.timestamp < proposal.endTime, "Proposal has ended!")
        let userVote: vote = {
            account = Call.caller,
            support = false}
        let updatedProposal = proposal{votesAgainst @ v = v + 1, votes @ vts = userVote :: vts}
        put(state{proposals[proposalId] = updatedProposal, totalVotes = state.totalVotes + 1})
        put(state{proposals[proposalId].hasVoted[Call.caller] = true})
        let activities : userActivities = Map.lookup_default(Call.caller, state.activities, {account = Call.caller, proposalsCreated = 0, proposalsExecuted = 0, voteCasted = 0})
        put(state{activities[Call.caller] = activities{proposalsCreated = activities.voteCasted + 1}})
        updatedProposal

    /**
     * @notice Executes a proposal if conditions are met.
     * @param proposalId The ID of the proposal to execute.
     */        

    public stateful entrypoint executeProposal(_proposalId: int) = 
        require(isMember(Call.caller), "You're not a member of this DAO")
        require(_proposalId < state.totalProposals, "Invalid proposal id")
        let _proposal = state.proposals[_proposalId]
        require(Chain.timestamp > _proposal.endTime, "Proposal has not ended!")
        require(_checkQuorum(_proposalId), "Quorum was not reached")
        _proposal{isExecuted = true}
        if(_proposal.proposalType == "updateName")
            _updateName(_proposal.info.name)
        if(_proposal.proposalType == "updateSocials")
            _updateSocials(_proposal.info.socials)
        if(_proposal.proposalType == "updateImage")
            _updateImage(_proposal.info.image)
        if(_proposal.proposalType == "addMember")
            _addMember(_proposal.target)
        elif(_proposal.proposalType == "removeMember")
            _removeMember(_proposal.target)
        elif(_proposal.proposalType == "quorum")
            _setQuorum(_proposal.value)
        elif(_proposal.proposalType == "voteTime")
            _setVotingTime(_proposal.value)
        elif(_proposal.proposalType == "transfer")
            Chain.spend(_proposal.target, _proposal.value)
        put(state{proposals[_proposalId] = _proposal})

    /**
    * @notice Allows sending funds to the DAO's contract address. These funds can be used for DAO operations or redistributed based on proposals.
    */
    public stateful payable entrypoint deposit() = 
        ()

    // Internal functions
    /**
    * @dev Internal function to check if a proposal has reached the required quorum.
    * @param _proposalId The ID of the proposal to check.
    * @return True if the proposal meets the quorum requirement, otherwise false.
    */
    stateful function _checkQuorum(_proposalId: int): bool = 
        let _proposal = state.proposals[_proposalId]
        let _totalVotes = _proposal.votesFor + _proposal.votesAgainst
        let _percentageVoted = _totalVotes * 100 / List.length(state.members)
        if (_percentageVoted > state.quorum)
            true
        else
            false
    /**
    * @dev Internal function to update the DAO's name.
    * @param _name The new name to set for the DAO.
    */
    stateful function _updateName(_name: string) = 
        put(state{name = _name})

    /**
    * @dev Internal function to update the social media links of the DAO.
    * @param _socials A list of new social URLs to set for the DAO.
    */
    stateful function _updateSocials(_socials: list(socialUrl)) = 
        put(state{socials = _socials})

    /**
     * @dev Internal function to update the image URL of the DAO.
     * @param _image The new image URL to set for the DAO.
     */
    stateful function _updateImage(_image: string) = 
        put(state{image = _image})
    /**
     * @dev Internal function to add a new member to the DAO.
     * @param newMember The address of the new member.
     */
    stateful function _addMember(newMember: address) = 
        if(!List.contains(newMember, state.members))
            put(state{members = newMember :: state.members})

    /**
     * @dev Internal function to remove a member from the DAO.
     * @param memberToRemove The address of the member to remove.
     */
    stateful function _removeMember(memberToRemove: address) = 
        let filteredMembers = List.filter((m) => m != memberToRemove, state.members)
        put(state{members = filteredMembers})

    /**
     * @dev Internal function to set a new quorum percentage for the DAO.
     * @param newQuorum The new quorum percentage.
     */
    stateful function _setQuorum(newQuorum: int) = 
        if(newQuorum > 0 && newQuorum =< 100)
            put(state{quorum = newQuorum})
        else
            abort("Invalid quorum value. Must be between 1 and 100.")

    /**
     * @dev Internal function to set a new voting time for proposals in the DAO.
     * @param newVotingTime The new duration of the voting period.
     */
    stateful function _setVotingTime(newVotingTime: int) = 
        if(newVotingTime > 0)
            put(state{votingTime = newVotingTime})
        else
            abort("Invalid voting time. Must be greater than 0.")


